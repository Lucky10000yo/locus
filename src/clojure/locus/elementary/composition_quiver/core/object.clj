(ns locus.elementary.composition-quiver.core.object
  (:require [locus.base.logic.core.set :refer :all]
            [locus.base.logic.limit.product :refer :all]
            [locus.base.logic.structure.protocols :refer :all]
            [locus.base.function.core.object :refer :all]
            [locus.base.partition.core.setpart :refer :all]
            [locus.quiver.relation.binary.product :refer :all]
            [locus.quiver.relation.binary.br :refer :all]
            [locus.quiver.relation.binary.sr :refer :all]
            [locus.quiver.base.core.protocols :refer :all]
            [locus.elementary.copresheaf.core.protocols :refer :all]
            [locus.quiver.binary.core.object :refer :all]
            [locus.elementary.two-quiver.core.object :refer :all]
            [locus.elementary.two-quiver.path.object :refer :all]
            [locus.quiver.ternary.core.object :refer :all]
            [locus.algebra.category.core.object :refer :all]
            [locus.algebra.semigroupoid.core.object :refer :all]))

; The topos of composition quivers is the topos of presheaves over the compositional quiver index
; category, which is a category generated by five morphisms between three objects. Compositional
; quivers are noteworthy for providing the presheaf theoretic account of categories. They form
; a topos that includes categories as well as their immediate generalisations: semigroupoids,
; magmoids, groupoids, partial magmoids, and so on. They are so generalized that they include
; epi-mono factorisations so that by this topos you can form epi-mono factorisations of functors
; between two categories using an intermediate composition quiver determined by a quotient
; and a subobject.

; The modus operandi of Locus is to represent structures using presheaves. It is important then
; that categories, which are one of the most basic and fundamental tools in presheaf theory,
; should be represented as presheaves. The topos of compositional quivers is our fundamental
; solution. In the broadest sense it consists of multisets of composition triples of a quiver.
; Categories are then just an important special case, used to form other topoi.

(deftype CompositionQuiver [quiver op]
  StructuredDiset
  (first-set [this] (first-set quiver))
  (second-set [this])

  StructuredQuiver
  (underlying-quiver [this] quiver)
  (source-fn [this] (source-fn quiver))
  (target-fn [this] (target-fn quiver))
  (transition [this e]
    (transition quiver e)))

(derive CompositionQuiver :locus.elementary.copresheaf.core.protocols/structured-quiver)

; Underlying relations and multirelations of composition quivers
(defmethod underlying-multirelation CompositionQuiver
  [^CompositionQuiver quiver] (underlying-multirelation (underlying-quiver quiver)))

(defmethod underlying-relation CompositionQuiver
  [^CompositionQuiver quiver] (underlying-relation (underlying-quiver quiver)))

(defmethod visualize CompositionQuiver
  [^CompositionQuiver quiver] (visualize (underlying-quiver quiver)))

; Common multimethods for composition quivers
(defmethod paths CompositionQuiver
  [^CompositionQuiver quiver]

  (morphisms (.-op quiver)))

(defmethod underlying-path-quiver CompositionQuiver
  [^CompositionQuiver quiver]

  (let [op (.-op quiver)]
    (->PathQuiver
     (paths quiver)
     (morphisms quiver)
     (objects quiver)
     (source-fn quiver)
     (target-fn quiver)
     (first-component-fn op)
     (second-component-fn op))))

; Compositional ternary quivers of category like structures
(defmulti compositional-ternary-quiver type)

(defmethod compositional-ternary-quiver CompositionQuiver
  [^CompositionQuiver quiver]

  (.-op quiver))

(defmethod compositional-ternary-quiver :locus.elementary.copresheaf.core.protocols/partial-magmoid
  [magmoid]

  (->TernaryQuiver
    (inputs magmoid)
    (outputs magmoid)
    first
    second
    (fn [[a b]]
      (magmoid (list a b)))))

; Conversion to ternary quivers
(defmethod to-ternary-quiver CompositionQuiver
  [^CompositionQuiver quiver] (compositional-ternary-quiver quiver))

(defmethod to-ternary-quiver :locus.elementary.copresheaf.core.protocols/partial-magmoid
  [magmoid] (compositional-ternary-quiver magmoid))

; Product and coproduct multimethods for the topos of compositional quivers
(defn compositional-quiver-product
  [& quivers]

  (->CompositionQuiver
    (apply quiver-product (map underlying-quiver quivers))
    (apply ternary-quiver-product (map compositional-ternary-quiver quivers))))

(defn compositional-quiver-coproduct
  [& quivers]

  (->CompositionQuiver
    (apply quiver-coproduct (map underlying-quiver quivers))
    (apply ternary-quiver-coproduct (map compositional-ternary-quiver quivers))))

(defmethod product CompositionQuiver
  [& quivers]

  (apply compositional-quiver-product quivers))

(defmethod coproduct CompositionQuiver
  [& quivers]

  (apply compositional-quiver-coproduct quivers))

; Multimethods to convert things into compositional quivers
(defmulti to-compositional-quiver type)

(defmethod to-compositional-quiver CompositionQuiver
  [^CompositionQuiver quiver] quiver)

(defmethod to-compositional-quiver :locus.elementary.copresheaf.core.protocols/partial-magmoid
  [magmoid]

  (->CompositionQuiver
    (underlying-quiver magmoid)
    (compositional-ternary-quiver magmoid)))

; Subobjects in the topos of compositional quivers
(defn compositional-subquiver
  [quiver new-paths new-morphisms new-objects]

  (->CompositionQuiver
    (subquiver (underlying-quiver quiver) new-morphisms new-objects)
    (ternary-subquiver (compositional-ternary-quiver quiver) new-paths new-morphisms)))

(defn restrict-quiver-composition
  [quiver new-paths]

  (->CompositionQuiver
    (underlying-quiver quiver)
    (wide-ternary-subquiver quiver new-paths)))

; Restrict the underlying quiver of a compositional quiver
(defn restrict-underlying-quiver
  [quiver new-morphisms new-objects]

  (->CompositionQuiver
    (subquiver (underlying-quiver quiver) new-morphisms new-objects)
    (full-ternary-subquiver (compositional-ternary-quiver quiver) new-morphisms)))

; Ontology of subobjects in the topos of compositional quivers
(defn compositional-subquiver?
  [quiver new-paths new-morphisms new-objects]

  (and
    (subquiver? (underlying-quiver quiver) new-morphisms new-objects)
    (ternary-subquiver? (compositional-ternary-quiver quiver) new-paths new-morphisms)))

; Congruences in the topos of compositional quivers
(defn compositional-quiver-congruence?
  [quiver path-partition morphism-partition object-partition]

  (and
    (quiver-congruence? (underlying-quiver quiver) morphism-partition object-partition)
    (ternary-quiver-congruence? (compositional-ternary-quiver quiver) path-partition morphism-partition)))

(defn quotient-compositional-quiver
  [quiver path-partition morphism-partition object-partition]

  (->CompositionQuiver
    (quotient-quiver (underlying-quiver quiver) morphism-partition object-partition)
    (ternary-quotient-quiver (compositional-ternary-quiver quiver) path-partition morphism-partition)))

; Ontology of compositional quivers
(defn compositional-quiver?
  [quiver]

  (= (type quiver) CompositionQuiver))

(defn compositionally-thin-composition-quiver?
  [quiver]

  (and
    (compositional-quiver? quiver)
    (thin-ternary-quiver? (compositional-ternary-quiver quiver))))

(defn algebraic-compositional-quiver?
  [quiver]

  (and
    (compositional-quiver? quiver)
    (at-quiver? (compositional-ternary-quiver quiver))))
